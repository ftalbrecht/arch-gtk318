# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk-doc
pkgver=1.25
pkgrel=1
pkgdesc="Documentation tool for public library API"
arch=('any')
license=('GPL' 'FDL')
depends=('docbook-xsl' 'yelp-tools' 'perl' 'source-highlight')
makedepends=('pkgconfig' 'jade' 'itstool' 'dblatex')
optdepends=('jade: SGML support'
            'dblatex: PDF support')
url="http://www.gtk.org/gtk-doc/"
source=(https://download.gnome.org/sources/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('1ea46ed400e6501f975acaafea31479cea8f32f911dca4dff036f59e6464fd42')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
