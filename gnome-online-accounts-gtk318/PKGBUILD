# Maintainer: Felix Schindler <aur at schindlerfamily dot de>
# Contributer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-accounts-gtk318
_pkgname=gnome-online-accounts
pkgver=3.18.2.1
pkgrel=1
pkgdesc="GNOME service to access online accounts"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL')
provides=("${_pkgname}=$pkgver")
conflicts=($_pkgname)
replaces=($_pkgname)
depends=('webkit2gtk-gtk318' 'json-glib' 'libnotify' 'rest' 'libsecret' 'telepathy-glib' 'krb5' 'gcr-gtk318')
makedepends=('intltool' 'libxslt' 'gobject-introspection' 'docbook-xsl')
optdepends=('gvfs-goa: Virtual file systems (e.g. OwnCloud)'
            'gvfs-google: Google Drive')
install=$_pkgname.install
source=(https://download.gnome.org/sources/$_pkgname/${pkgver:0:4}/$_pkgname-$pkgver.tar.xz)
sha256sums=('ca0a9c58ea5ac08437ed68fea9e8c3de69a8804e670ccf5a578c6786096ecfac')

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$_pkgname --disable-static \
    --enable-media-server --enable-kerberos --enable-lastfm
  make
}

package() {
  cd $_pkgname-$pkgver
  make -j1 DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
