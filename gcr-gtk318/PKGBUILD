# Maintainer: Felix Schindler <aur at schindlerfamily dot de>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gcr-gtk318
_pkgname=gcr
pkgver=3.18.0
pkgrel=1
pkgdesc="A library for bits of crypto UI and parsing"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL2')
conflicts=('gcr')
depends=('dconf' 'desktop-file-utils' 'gtk3-gtk318' 'hicolor-icon-theme' 'libgcrypt'
         'p11-kit')
makedepends=('intltool' 'gobject-introspection' 'python' 'vala' 'libxslt')
options=('!makeflags')
install=gcr.install
source=(http://download.gnome.org/sources/$_pkgname/${pkgver:0:4}/$_pkgname-$pkgver.tar.xz
        10-gcr.conf)
sha256sums=('d4d16da5af55148a694055835ccd07ad34daf0ad03bdad929bf7cad15637ce00'
            '5f2eda7175ae9f23ee0e09d2beceb24fd2f6daafd7bddfcc1c1f5a3734eb60fc')

prepare() {
  mkdir path
  ln -s /usr/bin/python2 path/python
}

build() {
  cd "$_pkgname-$pkgver"
  ./configure --prefix=/usr \
    --libexec=/usr/lib/gcr \
    --disable-static \
    --disable-update-mime \
    --disable-schemas-compile
  make
}

check() {
  cd "$_pkgname-$pkgver"
  PATH="$srcdir/path:$PATH" dbus-run-session make -k check || :
}

package() {
  cd "$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # gcr wants to lock some memory to prevent swapping out private keys
  # https://bugs.archlinux.org/task/32616
  # https://bugzilla.gnome.org/show_bug.cgi?id=688161
  install -Dm644 ../10-gcr.conf "$pkgdir/etc/security/limits.d/10-gcr.conf"
}

# vim:set ts=2 sw=2 et:
