# Maintainer: Felix Schindler <aur at schindlerfamily dot de>
# Contributor: Jan de Groot  <jgc@archlinux.org>

pkgname=libgweather-gtk318
_pkgname=libgweather
pkgver=3.18.1
pkgrel=1
pkgdesc="Provides access to weather information from the net"
arch=('i686' 'x86_64')
license=('LGPL')
provides=("${_pkgname}=$pkgver")
conflicts=($_pkgname)
replaces=($_pkgname)
depends=('libsoup' 'gtk3-gtk318' 'geocode-glib-gtk318')
makedepends=('intltool' 'gobject-introspection' 'vala')
options=('!emptydirs')
url="http://www.gnome.org/"
install=libgweather.install
source=(https://download.gnome.org/sources/$_pkgname/${pkgver:0:4}/$_pkgname-$pkgver.tar.xz)
sha256sums=('94b2292f8f7616e2aa81b1516befd7b27682b20acecbd5d656b6954990ca7ad0')

build() {
    cd "$_pkgname-$pkgver"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static
    make
}

package() {
    cd "$_pkgname-$pkgver"
    make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install
}
