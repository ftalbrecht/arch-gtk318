# Maintainer: Felix Schindler <aur at schindlerfamily dot de>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gnome-keyring-gtk318
_pkgname=gnome-keyring
conflicts=('gnome-keyring')
epoch=1
pkgver=3.18.3
pkgrel=1
pkgdesc="GNOME Password Management daemon"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('gcr-gtk318' 'libcap-ng')
makedepends=('intltool' 'gtk-doc-gtk318')
groups=('gnome')
options=('!emptydirs')
url="http://www.gnome.org"
install=gnome-keyring.install
source=(https://download.gnome.org/sources/$_pkgname/${pkgver:0:4}/$_pkgname-$pkgver.tar.xz)
sha256sums=('3f670dd61789bdda75b9c9e31e289bf7b1d23ba012433474790081ba7dc0ed98')

build() {
  cd "$_pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gnome-keyring \
      --with-pam-dir=/usr/lib/security --with-root-certs=/etc/ssl/certs \
      --disable-schemas-compile
  make
}

package() {
  cd "$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
