# Maintainer: Felix Schindler <aur at schindlerfmily dot de>
# COntributor: Jan de Groot <jgc@archlinux.org>

pkgname=libnotify-gtk318
_pkgname=libnotify
conflicts=('libnotify')
pkgver=0.7.6
pkgrel=2
pkgdesc="Library for sending desktop notifications"
arch=(i686 x86_64)
url="https://developer.gnome.org/notification-spec/"
license=(LGPL)
depends=(gdk-pixbuf2)
makedepends=(gtk3-gtk318 gobject-introspection git gnome-common-gtk318)
_commit=2f2c5649ef210b1dffeb46cddf062d20e1518ccf
source=("git://git.gnome.org/libnotify#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd $_pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $_pkgname
  ./configure --prefix=/usr --disable-static --enable-gtk-doc
  make
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir" install
}
