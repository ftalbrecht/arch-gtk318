# Maintainer: Felix Schindler <aur at schindlerfamily dot de>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=yelp-gtk318
_pkgname=yelp
pkgver=3.18.1
pkgrel=1
pkgdesc="A help browser for GNOME"
arch=('i686' 'x86_64')
provides=("${_pkgname}=$pkgver")
conflicts=($_pkgname)
replaces=($_pkgname)
depends=('webkit2gtk-gtk318' 'yelp-xsl-gtk318' 'libxslt' 'hicolor-icon-theme' 'xz' 'bzip2'
         'desktop-file-utils' 'dconf')
makedepends=('intltool' 'gtk-doc' 'itstool')
groups=('gnome')
license=('GPL')
options=('!emptydirs')
url="http://www.gnome.org"
install=yelp.install
source=(https://download.gnome.org/sources/$_pkgname/${pkgver:0:4}/$_pkgname-$pkgver.tar.xz)
sha256sums=('ba3a4eb4717c0ecf4a2e40eff0963fcd12c700c4fb80b83ecaad8b7032256880')

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
