# Maintainer: Felix Schindler <aur at schindlerfamily dot de>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server-gtk318
_pkgname=evolution-data-server
pkgver=3.18.5
pkgrel=1
pkgdesc="Centralized access to appointments and contacts"
arch=(i686 x86_64)
provides=("${_pkgname}=$pkgver")
conflicts=($_pkgname)
replaces=($_pkgname)
depends=(gnome-online-accounts-gtk318 nss krb5 libgweather-gtk318 libical db libgdata libphonenumber)
makedepends=(intltool gperf gobject-introspection vala gtk-doc boost)
install=$_pkgname.install
url="https://wiki.gnome.org/Apps/Evolution"
license=(GPL)
source=(https://download.gnome.org/sources/$_pkgname/${pkgver:0:4}/$_pkgname-$pkgver.tar.xz)
sha256sums=('a897c789925e86df31a1c78212b431cca13dc4d5a3d87e1fb3d507df3994e8e1')

build() {
  cd "$_pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-openldap=yes \
      --libexecdir=/usr/lib/evolution-data-server \
      --with-krb5=/usr --with-libdb=/usr --with-phonenumber=/usr \
      --enable-vala-bindings --disable-uoa \
      --enable-gtk-doc

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
