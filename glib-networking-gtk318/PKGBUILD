# Maintainer: Felix Schindler <aur at schindlerfamily dot de>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=glib-networking-gtk318
_pkgname=glib-networking
conflicts=('glib-networking')
pkgver=2.48.2
pkgrel=1
pkgdesc="Network-related giomodules for glib"
url="https://git.gnome.org/browse/glib-networking/"
arch=(i686 x86_64)
license=(GPL2)
depends=(glib2 libproxy gnutls ca-certificates gsettings-desktop-schemas-gtk318)
makedepends=(intltool)
source=(http://download.gnome.org/sources/$_pkgname/${pkgver:0:4}/$_pkgname-$pkgver.tar.xz)
sha256sums=('925c0c49d6b2b8b5695f2e33cd952d1dbb7d18d3f2f796413577719315bb3a84')

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/$_pkgname --disable-static \
    --disable-installed-tests
  make
}

check() {
  cd $_pkgname-$pkgver
  make -j1 -k check
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install testfiles_DATA=
}

# vim:set ts=2 sw=2 et:
